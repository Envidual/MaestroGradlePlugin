plugins {
    // Apply the Java Gradle plugin development plugin to add support for developing Gradle plugins
    id 'java-gradle-plugin'

    // Apply the Groovy plugin to add support for Groovy
    id 'groovy'

    id 'maven-publish'
}

ext {
    pluginVersion = '1.1.5'
}

group = 'org.envidual'
version = pluginVersion


repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    // Use the awesome Spock testing and specification framework
    testImplementation 'org.spockframework:spock-core:2.2-groovy-3.0'
}

gradlePlugin {
    // Define the plugin
    plugins {
        maestroTestingPlugin {
            id = 'org.envidual.maestroTesting'
            implementationClass = 'org.envidual.plugins.maestroTesting.MaestroPlugin'
            displayName = 'Maestro Testing'
            description = 'automatically run maestro tests'
        }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/envidual/maestro-plugin")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_TOKEN")
            }
        }
    }
    publications {
        mavenJava(MavenPublication){
            from(components.java)
            groupId = 'org.envidual'
            artifactId = 'maestrotesting'
            version = pluginVersion
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}
